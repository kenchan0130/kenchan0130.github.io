---
layout: post
title: Azure Active DirectoryのアプリケーションをTerraformで管理する
outline: >
  Azure Active Directory（Azure AD）には「エンタープライズアプリケーション」と「アプリ登録」と呼ばれる管理項目があります。
  特に、SAMLやOIDCを利用するアプリケーションを登録するなど、運用する際に使用されることが多いでしょう。
  今回はこれらをInfrastructure as Code（IaC）のデファクトスタンダードとなりつつあるTerraformで管理する方法を紹介します。
categories:
  - system-administration
tags:
  - Azure Active Directory
  - Azure CLI
  - IaC
  - Microsoft Graph API
  - Terraform
---

この記事は[FOLIO Advent Calendar 2022](https://adventar.org/calendars/7813)の12/6分の記事です。かなり遅れてサンタクロースがやってきてしまいました。

Azure Active Directory（Azure AD）には「エンタープライズアプリケーション」と「アプリ登録」と呼ばれる管理項目があります。
特に、SAMLやOIDCを利用するアプリケーションを登録するなど、運用する際に使用されることが多いでしょう。

今回はこれらをInfrastructure as Code（IaC）のデファクトスタンダードとなりつつあるTerraformで管理する方法を紹介します。

* TOC
{:toc}

## Azure ADの構成をコード管理

Azure ADでアプリケーションを管理する場合、通常、[Microsoft Azure Portal](https://portal.azure.com)（Azure Portal）と呼ばれるポータルサイト上で作業します。
GUIの操作であるため、直感的でわかりやすく、ほとんどの方がこちらで作業をしていることでしょう。

しかし、なぜその変更を加えたのかの記録が残らず、複数人で変更する場合、その操作が適切なものなのかどうかを確認することが難しいです。

インフラ構成をコード管理することで、これらの課題を解決または解消できます。

今回紹介するTerraformでは、Terraformの開発元であるHashiCorp社が管理する[Azure AD用のプロバイダ](https://registry.terraform.io/providers/hashicorp/azuread)を使用します。
このプロバイダは内部で[Microsoft Graph API](https://learn.microsoft.com/ja-jp/graph/api/resources/azure-ad-overview)を使用しているため、Microsoft Graph APIで操作できるAzure ADの項目は一通り操作できます。

## エンタープライズアプリケーションとアプリの登録について

アプリケーションをコード管理するにあたって、Azure Portalにおける管理項目でよく目にする「エンタープライズアプリケーション」と「アプリの登録」について理解しておく必要があります。

「アプリの登録」は、Azure ADに登録するSaaSなどのサービスプロバイダがMicrosoft Graph APIで要求するリソースへのアクセス管理や、サービスプロバイダがアプリケーションにアクセスするためのトークンを発行する方法などを定義します。
Azure ADでは**アプリケーションオブジェクト**と呼ばれ、後述のサービスプリンシパルオブジェクトのひな型となります。

「エンタープライズアプリケーション」は、SaaSアプリケーションのSAMLによるSSOやユーザーやグループのプロビジョニング、Azure ADのユーザーやグループのアサインなどが設定できます。
エンタープライズアプリケーションで一覧化されているオブジェクトは、Azure ADでは**サービスプリンシパルオブジェクト**と呼ばれます。

アプリケーションオブジェクトだけでは何も機能せず、実際にサービスプロバイダと認証情報のやりとりをするのがサービスプリンシパルオブジェクトです。

また、アプリケーションオブジェクトとサービスプリンシパルオブジェクトは、**1対多**の関係です。

詳しくは「[Azure Active Directory のアプリケーション オブジェクトとサービス プリンシパル オブジェクト](https://learn.microsoft.com/ja-jp/azure/active-directory/develop/app-objects-and-service-principals)」を参照してください。


## Terraformでアプリケーションを管理

今回は、アプリケーション管理で特に多く使用される、

* サービスプロバイダがAzure ADをIdPとしてSAMLを使用して認証
* サービスプロバイダがAzure ADをIdPとしてOIDCを使用して認証

の2つのユースケースをTerraformでコード管理してみます。

どちらのケースでも以下のように、Azure ADのプロバイダの設定をする必要があります。

```hcl
terraform {
  required_providers {
    azuread = {
      source  = "hashicorp/azuread"
      version = "2.31.0" # 執筆時点の最新バーション
    }
  }
}

provider "azuread" {
  tenant_id = "ここにAzure ADのテナントIDをいれる"
}
```

また、今回はすでにAzure AD上に存在する`kenchan0130`ユーザーのみがサービスプロバイダにログインできることとします。

## ユースケース1. サービスプロバイダがAzure ADをIdPとしてSAMLを使用して認証

今回は、手元にJamf Proのテナントを持っていたので、こちらのサービスプロバイダにログインできるように、Azure ADの構成をTerraform化します。

```
.
├── main.tf
└── provider.tf
```

`provider.tf` に前述したプロバイダーの設定、`main.tf` に今回のユースケースを構成します。
以下が最低限動作するコードです。

```hcl
# main.tf
resource "azuread_application" "jamf_pro" {
  display_name = "Jamf Pro by terraform"
  identifier_uris = ["api://example-jamf-pro"]

  web {
    redirect_uris = [
      "https://exmaple.jamfcloud.com/saml/SSO"
    ]
  }
}

resource "azuread_service_principal" "jamf_pro" {
  application_id                = azuread_application.jamf_pro.application_id
  app_role_assignment_required  = true
  preferred_single_sign_on_mode = "saml"

  feature_tags {
    enterprise            = true
    custom_single_sign_on = true
  }
}

data "azuread_user" "kenchan0130" {
  user_principal_name = "kenchan0130@exmaple.com"
}

resource "azuread_app_role_assignment" "jamf_pro" {
  for_each = toset([
    for user in [
      data.azuread_user.kenchan0130
    ] : user.object_id
  ])

  app_role_id         = "00000000-0000-0000-0000-000000000000" # default role ID
  resource_object_id  = azuread_service_principal.jamf_pro.object_id
  principal_object_id = each.value
}

resource "tls_private_key" "jamf_pro_cert" {
  algorithm = "RSA"
  rsa_bits  = 2048
}

resource "tls_self_signed_cert" "jamf_pro" {
  allowed_uses          = ["client_auth", "server_auth"]
  private_key_pem       = tls_private_key.jamf_pro_cert.private_key_pem
  validity_period_hours = 4321

  subject {
    common_name = azuread_application.jamf_pro.display_name
  }
}

data "tls_certificate" "jamf_pro" {
  content = tls_self_signed_cert.jamf_pro.cert_pem
}

resource "azuread_service_principal_certificate" "jamf_pro" {
  service_principal_id = azuread_service_principal.jamf_pro.id
  type                 = "AsymmetricX509Cert"
  value                = tls_self_signed_cert.jamf_pro.cert_pem
  end_date             = replace(tls_self_signed_cert.jamf_pro.validity_end_time, "/\\.[0-9]+\\+/", "+") # ミリ秒が受け付けられないため取り除いている

  provisioner "local-exec" {
    command = <<-SHELL
      az ad sp update \
        --id ${self.service_principal_id} \
        --set preferredTokenSigningKeyThumbprint=${data.tls_certificate.jamf_pro.certificates[0].sha1_fingerprint}
SHELL
  }

  lifecycle {
    create_before_destroy = true
  }
}
```

それぞれのリソースについて、行っている内容や注意点などを見ていきます。

### SAMLの構成に伴うazuread_applicationリソース

[azuread_application](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/application)は、Azure ADのアプリケーションオブジェクトを作成します。

```hcl
resource "azuread_application" "jamf_pro" {
  display_name = "Jamf Pro by terraform"
  identifier_uris = ["api://example-jamf-pro"]

  web {
    redirect_uris = [
      "https://exmaple.jamfcloud.com/saml/SSO"
    ]
  }
}
```

`web`ブロックの`redirect_uris`は、SAMLのAssertion Consumer Service（ACS）に該当します。

`identifier_uris`はSAMLのエンティティIDに該当します。エンティティIDは[RFC7522](https://www.rfc-editor.org/rfc/rfc7522)でURIであるであることが定められています。
また、エンティティIDはAzure ADのテナント内で一意、つまり重複しないように設定する必要があります。

#### identifier_urisを設定するとアプリケーションオブジェクトが作成できない問題

`azuread_application`の`identifier_uris`に`https://exmaple.jamfcloud.com/saml/metadata` を設定しようとすると`terraform apply`時に以下のようなエラーが発生しました。

```
╷
│ Error: Could not create application
│
│   with azuread_application.jamf_pro,
│   on main.tf line 1, in resource "azuread_application" "jamf_pro":
│    1: resource "azuread_application" "jamf_pro" {
│
│ ApplicationsClient.BaseClient.Post(): unexpected status 400 with OData error: HostNameNotOnVerifiedDomain: Values of identifierUris property must use a
│ verified domain of the organization or its subdomain: 'https://example.jamfcloud.com/saml/metadata'
╵
```

原因は、「[シングル テナント アプリケーションの AppId URI には、既定のスキームまたは検証済みドメインを使用する必要があります](https://learn.microsoft.com/ja-jp/azure/active-directory/develop/reference-breaking-changes#appid-uri-in-single-tenant-applications-will-require-use-of-default-scheme-or-verified-domains)」です。
つまり、設定できるのは、検証済みのドメインのURIと`api://`で始まるデフォルトスキームのURIのみです。
Azure Portalでアプリケーションオブジェクトを作成するとこの制約に引っかからないため、API経由のみの制約であると思われます。

そのため今回の例では、`api://example-jamf-pro`というデフォルトスキームURIを採用しました。

もし、どうしても検証済みではないURIをエンティティIDとして使用したい場合は、2つの解決方法があります。

1つ目は、2回`terraform apply`を実行する方法です。

1回目は、`identifier_uris`は設定せずに`terraform apply`を実行します。

```hcl
resource "azuread_application" "jamf_pro" {
  display_name = "Jamf Pro by terraform"

  web {
    redirect_uris = [
      "https://exmaple.jamfcloud.com/saml/SSO"
    ]
  }
}
```

アプリケーションオブジェクトが作成されたら、`identifier_uris`に検証されていないドメインを含むURIを設定し、`terraform apply`を実行します。

```hcl
resource "azuread_application" "jamf_pro" {
  display_name = "Jamf Pro by terraform"
  identifier_uris = ["https://example.jamfcloud.com/saml/metadata"]

  web {
    redirect_uris = [
      "https://exmaple.jamfcloud.com/saml/SSO"
    ]
  }
}
```

この方法は、どこインフラでも同じ構成を実現するというIaCの目的から若干外れてしまいますが、特別なハックをしなくても良いというのがメリットではあります。

2つ目は、`azuread_application`リソースでは`identifier_uris`を設定せずに、別のリソースで`identifier_uris`を設定する方法です。

```hcl
resource "azuread_application" "jamf_pro" {
  display_name = "Jamf Pro by terraform"

  web {
    redirect_uris = [
      "https://onishidev.jamfcloud.com/saml/SSO"
    ]
  }

  lifecycle {
    ignore_changes = [
      identifier_uris
    ]
  }
}

resource "null_resource" "application_jamf_pro_identifier_uris" {
  # triggersに設定されているされている値が変更された場合、再度local-execが実行される
  triggers = {
    application_id  = azuread_application.jamf_pro.application_id
    # triggersはobject(string)の型を要求するため、スペース文字でjoinしている
    identifier_uris = join(" ", [
      "https://exmaple.jamfcloud.com/saml/metadata"
    ])
  }

  provisioner "local-exec" {
    command = <<-SHELL
      az ad app update --id ${self.triggers.application_id} --identifier-uris ${self.triggers.identifier_uris}
SHELL
  }
}
```

`azuread_application`リソースでは、`identifier_uris`は設定しません。
合わせて別のリソースで更新されるため、`lifecycle`ブロックの`ignore_changes`で`identifier_uris`を指定しておきます。

`null_resource`リソースの`local-exec`の`provisioner`を使うことで、ローカルの実行ファイルを呼び出せます。
これを使って、Azureのリソースを操作できるCLIである[`az`](https://learn.microsoft.com/ja-jp/cli/azure/reference-index)コマンドを介してアプリケーションオブジェクトの`identifier_uris`を更新しています。[^az-command-alternative]

[^az-command-alternative]: なるべく依存を少なくするために[`az`](https://learn.microsoft.com/ja-jp/cli/azure/reference-index)コマンドを使わずに、Microsoft Graph APIを直接呼び出しても問題ありません。

実行環境に依存してしまう点がデメリットですが、何が起こるのかがtfファイルから読み取れるメリットがあります。

アプリケーションオブジェクトの更新で検証されていないドメインを含むURIを設定できるなら[^update-default-scheme-validation-via-api]、作成時にも設定させてほしいです。どうしてこんな変更入れてしまったの、Microsoftさん。

[^update-default-scheme-validation-via-api]: 更新APIを経由して、`api://example.jamfcloud.com`のように検証されていないドメインを含むデフォルトスキームを設定するとエラーになる

### SAMLの構成に伴うazuread_service_principalリソース

[azuread_service_principal](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/service_principal)は、Azure ADのサービスプリンシパルオブジェクトを作成します。

```hcl
resource "azuread_service_principal" "jamf_pro" {
  application_id                = azuread_application.jamf_pro.application_id
  app_role_assignment_required  = true
  preferred_single_sign_on_mode = "saml"

  feature_tags {
    enterprise            = true
    custom_single_sign_on = true
  }
}
```

`app_role_assignment_required`が無効の場合（デフォルトでは無効）はテナントに存在するすべてのアカウントでログインできます。
今回のユースケースのようにサービスプロバイダにログインできる人を制限したい場合は、`app_role_assignment_required`を有効にする必要があります。

また、`preferred_single_sign_on_mode`を`saml`にするだけでは、SAML認証ができません。
`feature_tags`ブロックの`custom_single_sign_on`も合わせて有効にする必要があります。

合わせて、`feature_tags`ブロックの`enterprise`を有効にすることで、Azure Portal上でエンタープライズアプリケーションとして検索できるため、特段理由がない限りは有効にしておくと良いでしょう。

### SAMLの構成に伴うazuread_app_role_assignmentリソース

[azuread_app_role_assignment](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/app_role_assignment)は、リソースアプリケーションで定義されているアプリケーションのロールを、プリンシパル（ユーザー、グループ、またはサービスプリンシパル）に紐付けるためのリソースです。

```hcl
data "azuread_user" "kenchan0130" {
  user_principal_name = "kenchan0130@exmaple.com"
}

resource "azuread_app_role_assignment" "jamf_pro" {
  for_each = toset([
    for user in [
      data.azuread_user.kenchan0130
    ] : user.object_id
  ])

  app_role_id         = "00000000-0000-0000-0000-000000000000" # default role ID
  resource_object_id  = azuread_service_principal.jamf_pro.object_id
  principal_object_id = each.value
}
```

`azuread_application`の`app_role`ブロックでアプリケーションのロールを定義できます。
たとえは、ロールによってサービスプロバイダに渡す値を動的に変更することで、権限管理を柔軟に行えます。

今回特にロールは定義しておらず、サービスプロバイダにログインできる人を制限したいだけですので、デフォルトロールを使用してサービスプリンシパルオブジェクトとユーザーオブジェクトの紐付けを行っています。

往々にして、サービスプリンシパルオブジェクトには複数のユーザーおよびグループオブジェクトを紐付けたくなります。そのため、今回は`for_each`を使用して複数のユーザーおよびグループオブジェクトを紐付けれるようにしてみました。

### SAMLの構成に伴うazuread_service_principal_certificateリソース

[azuread_service_principal_certificate](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/service_principal_certificate)は、Azure ADのサービスプリンシパルオブジェクトに紐付ける署名証明書を設定します。

Terraformだけで完結させたい場合は、[tls_private_key](https://registry.terraform.io/providers/hashicorp/tls/latest/docs/resources/private_key)と[tls_self_signed_cert](https://registry.terraform.io/providers/hashicorp/tls/latest/docs/resources/self_signed_cert)のリソースを使用することで証明書を発行できます。

```hcl
resource "tls_private_key" "jamf_pro_cert" {
  algorithm = "RSA"
  rsa_bits  = 2048
}

resource "tls_self_signed_cert" "jamf_pro" {
  allowed_uses          = ["client_auth", "server_auth"]
  private_key_pem       = tls_private_key.jamf_pro_cert.private_key_pem
  validity_period_hours = 4321

  subject {
    common_name = azuread_application.jamf_pro.display_name
  }
}

data "tls_certificate" "jamf_pro" {
  content = tls_self_signed_cert.jamf_pro.cert_pem
}

resource "azuread_service_principal_certificate" "jamf_pro" {
  service_principal_id = azuread_service_principal.jamf_pro.id
  type                 = "AsymmetricX509Cert"
  value                = tls_self_signed_cert.jamf_pro.cert_pem
  end_date             = replace(tls_self_signed_cert.jamf_pro.validity_end_time, "/\\.[0-9]+\\+/", "+") # ミリ秒が受け付けられないため取り除いている

  provisioner "local-exec" {
    command = <<-SHELL
      az ad sp update \
        --id ${self.service_principal_id} \
        --set preferredTokenSigningKeyThumbprint=${data.tls_certificate.jamf_pro.certificates[0].sha1_fingerprint}
SHELL
  }

  lifecycle {
    create_before_destroy = true
  }
}
```

`lifecycle`ブロックの`create_before_destroy`を有効にすることで、署名証明書の更新があった場合でも先に新しい署名証明書がサービスプリンシパルオブジェクトに紐付き、アクティブになるため、サービスプリンシパルオブジェクトに紐付いている署名証明書がない状態を作らず済むため、SAMLを使用した認証のダウンタイムをなくせます。

#### azuread_service_principal_certificateで署名証明書を設定してもアクティブにならない問題

`azuread_service_principal_certificate`リソースでサービスプリンシパルオブジェクトに署名証明書を紐付けたとしても、証明書が有効になっておらず**非アクティブ**の状態です。

残念ながら、署名証明書をアクティブにするTerraformリソースは存在しません。
執筆現在、サービスプリンシパルオブジェクトにPATCHリクエストを投げることでしか署名証明書をアクティブにできないためです。端的に言うと、Microsoft Graph APIの作りがイマイチなのです。

```hcl
provisioner "local-exec" {
  command = <<-SHELL
      az ad sp update \
        --id ${self.service_principal_id} \
        --set preferredTokenSigningKeyThumbprint=${data.tls_certificate.jamf_pro.certificates[0].sha1_fingerprint}
SHELL
}
```

`null_resource`以外にも、すべてのリソースで`local-exec`の`provisioner`を使うことで、ローカルの実行ファイルを呼び出せます。
これを使って、Azureのリソースを操作できるCLIである[`az`](https://learn.microsoft.com/ja-jp/cli/azure/reference-index)コマンドを介してサービスプリンシパルオブジェクトの`preferredTokenSigningKeyThumbprint`を更新します。[^az-command-alternative]

`preferredTokenSigningKeyThumbprint`に証明書のThumbprintが設定されると、サービスプリンシパルオブジェクトに紐付いている署名証明書がアクティブになります。

#### tls_private_keyリソースのprivate_key_pemがtfstateに保存されてしまう問題

例示している構成の場合、tfstateファイルに秘密鍵の情報が残ってしまいます。
そのため、tfstateファイルを安全に取り扱うなどの対応が必要です。

tfstateに秘密情報を残したくない場合は、

* Terraform外で証明書を発行して証明書を引数として受け取る
* 証明書の発行をAzure ADに任せる

の2つの方法が考えられます。

##### 方法1. Terraform外で証明書を発行して証明書を引数として受け取る

```hcl
variable "self_signed_cert_pem" {
  type = string
  default = "PEM形式の自己証明書"
}

data "tls_certificate" "jamf_pro" {
  content = var.self_signed_cert_pem
}

resource "azuread_service_principal_certificate" "jamf_pro" {
  service_principal_id = azuread_service_principal.jamf_pro.id
  type                 = "AsymmetricX509Cert"
  value                = var.self_signed_cert_pem
  end_date_relative    = data.tls_certificate.jamf_pro.not_after

  provisioner "local-exec" {
    command = <<-SHELL
      az ad sp update \
        --id ${self.service_principal_id} \
        --set preferredTokenSigningKeyThumbprint=${data.tls_certificate.jamf_pro.certificates[0].sha1_fingerprint}
SHELL
  }

  lifecycle {
    create_before_destroy = true
  }
}
```

`variable`ブロックを使うことで、引数として証明書を受け取れます。
これでtfstateに秘密情報が入らなくなります。
ただ、Terraformで秘密鍵を管理しなくなっただけですので、外部で安全に秘密鍵を管理する方法を検討する必要はあります。

##### 方法2. 証明書の発行をAzure ADに任せる

Microsoft Graph APIには[サービスプリンシパルに紐づく署名証明書を作成するAPI](https://learn.microsoft.com/ja-jp/graph/api/serviceprincipal-addtokensigningcertificate)が存在します。
しかし、Azure ADのTerraformプロバイダにはこのAPIを使用したリソースが存在しません。 [Issue](https://github.com/hashicorp/terraform-provider-azuread/issues/732)としては挙がっていますが、まだ解決はされていません。

```hcl
resource "null_resource" "service_principal_certificate_jamf_pro" {
  triggers = {
    service_principal_jamf_pro_id = azuread_service_principal.jamf_pro.id
  }

  provisioner "local-exec" {
    command = <<-SHELL
      thumbprint=$(az rest -m POST \
        --headers Content-Type=application/json \
        -u "https://graph.microsoft.com/v1.0/servicePrincipals/${self.triggers.service_principal_jamf_pro_id}/addTokenSigningCertificate" \
        --query "thumbprint" \
        -o tsv
      )
      echo "Certificate Thumbprint: $${thumbprint} [id=${self.triggers.service_principal_jamf_pro_id}]"
      az ad sp update \
        --id ${self.triggers.service_principal_jamf_pro_id} \
        --set preferredTokenSigningKeyThumbprint=$${thumbprint}
SHELL
  }
}
```

`null_resource`リソースの`local-exec`の`provisioner`を使うことで、ローカルの実行ファイルを呼び出せます。
これを使って、Azureのリソースを操作できるCLIである[`az`](https://learn.microsoft.com/ja-jp/cli/azure/reference-index)コマンドを介して`addTokenSigningCertificate`APIの実行とサービスプリンシパルオブジェクトの`preferredTokenSigningKeyThumbprint`を更新します。[^az-command-alternative]

2つのコマンドを実行しているため、`preferredTokenSigningKeyThumbprint`の更新で失敗してしまうと整合性が合わなくなる可能性がある点に注意が必要です。

## ユースケース2. サービスプロバイダがAzure ADをIdPとしてOIDCを使用して認証

今回は、Microsoft社がチュートリアルとして出している[OIDCを使用したNode.jsのWebアプリケーション](https://github.com/AzureADQuickStarts/AppModelv2-WebApp-OpenIDConnect-nodejs)のサービスプロバイダにログインできるように、Azure ADの構成をTerraform化します。

```
.
├── main.tf
└── provider.tf
```

`provider.tf` に前述したプロバイダーの設定、`main.tf` に今回のユースケースを構成します。
以下が最低限動作するコードです。

```hcl
# main.tf
data "azuread_application_published_app_ids" "well_known" {}

data "azuread_service_principal" "msgraph" {
  application_id = data.azuread_application_published_app_ids.well_known.result.MicrosoftGraph
}

locals {
  oidc_app_msgraph_api_scopes = [
    "openid",
    "profile",
    "offline_access",
    "Mail.Read"
  ]
}

resource "azuread_application" "oidc_app" {
  display_name = "Azure Active Directory OIDC Node.js web app sample by terraform"

  web {
    redirect_uris = [
      "http://localhost:3000/auth/openid/return"
    ]

    implicit_grant {
      id_token_issuance_enabled = true
    }
  }

  required_resource_access {
    resource_app_id = data.azuread_service_principal.msgraph.application_id

    dynamic "resource_access" {
      for_each = local.oidc_app_msgraph_api_scopes
      content {
        id   = azuread_service_principal.msgraph.oauth2_permission_scope_ids[resource_access.value]
        type = "Scope"
      }
    }
  }
}

resource "azuread_application_password" "oidc_app" {
  application_object_id = azuread_application.oidc_app.object_id
  display_name          = "Webアプリケーションで使用するシークレット"
  end_date_relative     = "2400h30m"
}

resource "azuread_service_principal" "oidc_app" {
  application_id                = azuread_application.oidc_app.application_id
  app_role_assignment_required  = true
  preferred_single_sign_on_mode = "oidc"

  feature_tags {
    enterprise = true
  }
}

data "azuread_user" "kenchan0130" {
  user_principal_name = "kenchan0130@example.com"
}

resource "azuread_app_role_assignment" "oidc_app" {
  for_each = toset([
    for user in [
      data.azuread_user.kenchan0130
    ] : user.object_id
  ])

  app_role_id         = "00000000-0000-0000-0000-000000000000" # default role ID
  resource_object_id  = azuread_service_principal.oidc_app.object_id
  principal_object_id = each.value
}

resource "azuread_service_principal_delegated_permission_grant" "oidc_app" {
  service_principal_object_id          = azuread_service_principal.oidc_app.object_id
  resource_service_principal_object_id = data.azuread_service_principal.msgraph.object_id
  claim_values                         = local.oidc_app_msgraph_api_scopes
}
```

それぞれのリソースについて、行っている内容やハマった内容などを見ていきます。

### OIDCの構成に伴うazuread_applicationリソース

[azuread_application](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/application)は、Azure ADのアプリケーションオブジェクトを作成します。

```hcl
data "azuread_application_published_app_ids" "well_known" {}

data "azuread_service_principal" "msgraph" {
  application_id = data.azuread_application_published_app_ids.well_known.result.MicrosoftGraph
}

locals {
  oidc_app_msgraph_api_scopes = [
    "openid",
    "profile",
    "offline_access",
    "Mail.Read"
  ]
}

resource "azuread_application" "oidc_app" {
  display_name = "Azure Active Directory OIDC Node.js web app sample by terraform"

  web {
    redirect_uris = [
      "http://localhost:3000/auth/openid/return"
    ]

    implicit_grant {
      id_token_issuance_enabled = true
    }
  }

  required_resource_access {
    resource_app_id = data.azuread_service_principal.msgraph.application_id

    dynamic "resource_access" {
      for_each = local.oidc_app_msgraph_api_scopes
      content {
        id   = azuread_service_principal.msgraph.oauth2_permission_scope_ids[resource_access.value]
        type = "Scope"
      }
    }
  }
}
```

`web`ブロックの`redirect_uris`は、OIDCのリダイレクトURIに該当します。

今回のサービスプロバイダはOIDCのハイブリッド フローを有効化するように求められているため、`implicit_grant`ブロックの`id_token_issuance_enabled`を有効にしています。

また、サービスプロバイダはスコープとして、

* `openid`
* `profile`
* `offline_access`
* `https://graph.microsoft.com/mail.read`

を要求しています。
これらのアクセス許可を定義するため、`required_resource_access`ブロックでMicrosoft Graph APIで定義されているスコープを許可しています。

Microsoft Graph APIではUUIDでスコープを定義しますが、UUIDが扱いづらいため、Azure ADのTerraformプロバイダにはこれらをうまく参照するためのデータソースが用意されています。

[azuread_application_published_app_ids](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/data-sources/application_published_app_ids)は、Azure ADのTerraformプロバイダが非公式に用意している[^unofficial-published-apps]、Microsoft社がAzure ADにすでに用意しているアプリケーションのUUID一覧です。

[^unofficial-published-apps]: Azure ADのTerraformのプロバイダが使用している[hamilton](https://github.com/manicminer/hamilton)ライブラリで[定義](https://github.com/manicminer/hamilton/blob/main/environments/published.go)されている

[azuread_service_principal](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/data-sources/service_principal)は、すでにAzure ADに存在するサービスプリンシパルオブジェクトを参照するためデータソースです。

これらのデータソースにより、比較的簡単に許可するスコープを定義できます。

ちなみに、スコープが複数あったため、`resource_access`ブロックを`dynamic`ブロックで記載していますが、以下と同義です。

```hcl
required_resource_access {
  resource_app_id = data.azuread_service_principal.msgraph.application_id

  resource_access {
    id   = data.azuread_service_principal.msgraph.oauth2_permission_scope_ids.openid
    type = "Scope"
  }

  resource_access {
    id   = data.azuread_service_principal.msgraph.oauth2_permission_scope_ids.profile
    type = "Scope"
  }

  resource_access {
    id   = data.azuread_service_principal.msgraph.oauth2_permission_scope_ids.offline_access
    type = "Scope"
  }

  resource_access {
    id   = data.azuread_service_principal.msgraph.oauth2_permission_scope_ids["Mail.Read"]
    type = "Scope"
  }
}
```

### OIDCの構成に伴うazuread_application_passwordリソース

[azuread_application_password](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/application_password)は、Azure ADのアプリケーションオブジェクトに紐付く、クライアントクレデンシャルフローで使用する、クライアントシークレットを作成します。

```hcl
resource "azuread_application_password" "oidc_app" {
  application_object_id = azuread_application.oidc_app.object_id
  display_name          = "Webアプリケーションで使用するシークレット"
  end_date_relative     = "2400h30m"
}
```

`azuread_application_password`リソースを使用すると、tfstateファイルにクライアントシークレットの情報が残ってしまいます。
そのため、tfstateファイルを安全に取り扱うなどの対応が必要です。

tfstateに秘密情報を残したくない場合は、Terraformを経由せずにAzure PortalやMicrosoft Graph APIを利用してクライアントシークレットを発行すると良いでしょう。

### OIDCの構成に伴うazuread_service_principalリソース

[azuread_service_principal](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/service_principal)は、Azure ADのサービスプリンシパルオブジェクトを作成します。

```hcl
resource "azuread_service_principal" "oidc_app" {
  application_id                = azuread_application.oidc_app.application_id
  app_role_assignment_required  = true
  preferred_single_sign_on_mode = "oidc"

  feature_tags {
    enterprise = true
  }
}
```

`app_role_assignment_required`が無効の場合（デフォルトでは無効）はテナントに存在するすべてのアカウントでログインできます。
今回のユースケースのようにサービスプロバイダにログインできる人を制限したい場合は、`app_role_assignment_required`を有効にする必要があります。

`preferred_single_sign_on_mode`を`oidc`にすると、Azure Portal上のエンタープライズアプリケーションの「シングル サインオン」項目に、「アプリ登録」ページへのリンクとJWTのクレームマッピングのカスタム構成ページへのリンクが表示されます。

`feature_tags`ブロックの`enterprise`を有効にすることで、Azure Portal上でエンタープライズアプリケーションとして検索できるため、特段理由がない限りは有効にしておくと良いでしょう。

### OIDCの構成に伴うazuread_app_role_assignmentリソース

こちらについては[SAMLの構成に伴うazuread_service_principal_certificateリソース](#samlの構成に伴うazuread_service_principal_certificateリソース)と同様のため省略します。

### OIDCの構成に伴うazuread_service_principal_delegated_permission_grantリソース

[azuread_service_principal_delegated_permission_grant](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs/resources/service_principal_delegated_permission_grant)は、Azure ADのサービスプリンシパルオブジェクトのAPIアクセス許可の権限委譲が設定できます。

```hcl
locals {
  oidc_app_msgraph_api_scopes = [
    "openid",
    "profile",
    "offline_access",
    "Mail.Read"
  ]
}

resource "azuread_service_principal_delegated_permission_grant" "oidc_app" {
  service_principal_object_id          = azuread_service_principal.oidc_app.object_id
  resource_service_principal_object_id = data.azuread_service_principal.msgraph.object_id
  claim_values                         = local.oidc_app_msgraph_api_scopes
}
```

今回のサービスプロバイダはスコープとして、

* `openid`
* `profile`
* `offline_access`
* `https://graph.microsoft.com/mail.read`

を要求しており、`azuread_application`リソースの`required_resource_access`でAPIアクセスの許可を定義していました。

しかし、実際にこのスコープにアクセスするには、アプリケーションオブジェクトではなく、サービスプリンシパルオブジェクトごとに管理者の同意が必要です。

![OIDCでログインする際に管理者権限でAPIアクセスの許可を委任してない場合に表示されるダイアログ]({% asset_path oidc_admin_privilege_delegation_dialog.png %})

`azuread_service_principal_delegated_permission_grant`リソースでは、この管理者の同意を設定できます。

`claim_values`は、`required_resource_access`のようにUUIDではなく、クレームのキーを指定します。
Microsoft Graph APIの一貫性がなくて使いづらい点がにじみ出てきてしまっているのが残念ですが、今回の例ように`locals`ブロックなどで、変数としてスコープを定義すると扱いやすくなります。

## クレームマッピングについて

SAMLではアサーション、OIDCではJWTクレームを介して、サービスプロバイダに値を渡したい場合があります。
その際は、クレームマッピングが使用できます。

以下は、SAMLの構成でクレームマッピングを使い、NameIDにディスプレイ名を割り当てた例です。

```hcl
resource "azuread_claims_mapping_policy" "jamf_pro" {
  display_name = "Jamf Pro Custom Claim Policy"
  definition = [
    jsonencode(
      {
        ClaimsMappingPolicy = {
          ClaimsSchema = [
            {
              ID            = "displayname"
              SamlClaimType = "http://schemas.xmlsoap.org/ws/2005/05/identity/claims/nameidentifier"
              Source        = "user"
            }
          ]
          IncludeBasicClaimSet = "true"
          Version              = 1
        }
      }
    )
  ]
}

resource "azuread_service_principal_claims_mapping_policy_assignment" "jamf_pro" {
  claims_mapping_policy_id = azuread_claims_mapping_policy.jamf_pro.id
  service_principal_id     = azuread_service_principal.jamf_pro.id
}
```

しかし、このAPIを使用すると、Azure Portal上で現在のクレームマッピングの情報が表示されず、クレームの変更もできなくなってしまいます。

![Azure PortalにおけるSAMLの属性とクレームの変更画面]({% asset_path saml_attributes_and_claims_at_azure_portal.png %})

[公式ドキュメント](https://learn.microsoft.com/ja-jp/azure/active-directory/develop/active-directory-claims-mapping)にも、

> これは、Azure portal で提供しているクレームのカスタマイズの後継機能です。 同一のアプリケーションに対し、このドキュメントで詳述する Microsoft Graph または Powershell を利用した方法に加えて、Azure portal でもクレームをカスタマイズした場合、そのアプリケーションのトークンでは、Azure portal での構成は無視されます。 このドキュメントで詳しく説明した方法で行った構成は、ポータルには反映されません。

と記載されており、仕様のようです。

これはか運用上支障が出る可能性があるため、注意が必要です。

## 終わりに

今回はTerraformを使用して、

* サービスプロバイダがAzure ADをIdPとしてSAMLを使用して認証
* サービスプロバイダがAzure ADをIdPとしてOIDCを使用して認証

の構築例を紹介しました。

これは最低限の設定であったため、その他にも設定したい項目がある場合は[Terraformのプロバイダの公式ドキュメント](https://registry.terraform.io/providers/hashicorp/azuread/latest/docs)や[Microsoft Graphの公式ドキュメント](https://learn.microsoft.com/ja-jp/graph/api/resources/azure-ad-overview)を参考に対応してみると良いと思います。
